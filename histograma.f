C SUBROUTINA QUE GENERA UN HISTOGRAMA
C 
C
C      INPUT: XDATA(NDAT)     ORIGINAL DATA
C             XA              LOWER LIMIT
C             XB              UPPER LIMIT
C             NBOX           NUMBER OF BOXES
C
C      OUTPUT: XHIS           CENTRAL VALUE OF THE COLUMN
C              VHIS            COLUMN VALUE
C           ERRHIS           BINOMIAL ESTIMATE OF THE ERROR
C           BOXSIZE           SIZE OF EACH BOX
C
C             IERR            =0, EVERYTHING ok
C                             =1, XA>XB
C                             =2, IF THERE ARE NO POINTS IN THE INTERVAL
C
C OUTPUT IS NORMALIZED TO THE NUMBER OF POINTS WHICH FALL 
C IN THE INTERVAL

       SUBROUTINE HISTOGRAMA(NDAT,XDATA,XA,XB,NBOX,XHIS,VHIS,ERRHIS,
     1 BOXSIZE,IERR)
       IMPLICIT NONE

C INPUT/OUTPUT VARIABLES

       INTEGER NDAT,NBOX
       DOUBLE PRECISION XDATA(NDAT),XA,XB
       DOUBLE PRECISION XHIS(NBOX),VHIS(NBOX),ERRHIS(NBOX)
       INTEGER IERR
C 
       INTEGER I,IBOX,ICOUNT
       DOUBLE PRECISION BOXSIZE

       IF (XA.GE.XB) THEN 
          IERR=1
          RETURN
       ENDIF
C BOX SIZE
         BOXSIZE=(XB-XA)/NBOX

C COUNTS NUMBER OF POINTS WITHIN THE INTERVAL XA,XB
       ICOUNT=0

C SETS ALL TO ZERO
       DO I=1,NBOX
          VHIS(I)=0
          ERRHIS(I)=0
       ENDDO

C WE RUN THROUGH THE DATASET
       DO I=1,NDAT
C CHECKS IF DATA LIES WITHIN XA,XB
         IF (XDATA(I).GE.XA.AND.XDATA(I).LE.XB) THEN 
            IBOX=INT((XDATA(I)-XA)/BOXSIZE)+1
C PUTS XB INTO THE LAST BOX, IF NEEDED
            IF (IBOX.EQ.NBOX+1) IBOX=NBOX 

            VHIS(IBOX)=VHIS(IBOX)+1
            ICOUNT=ICOUNT+1
         ENDIF
        ENDDO

        IF (ICOUNT.EQ.0) THEN 
           IERR=2
           RETURN
        ENDIF

        IERR=0
        PRINT*,"ACCEPTED:",ICOUNT,
     1  " OUT OF:",NDAT

        DO I=1,NBOX
c CENTRAL VALUE OF THE BAR
           XHIS(I)=XA+BOXSIZE/2.D0+(I-1)*BOXSIZE
C  ERROBAR, STANDARD DEVIATION OF CORRESPONDING BINOMIAL
           ERRHIS(I)=SQRT(VHIS(I)/ICOUNT*(1.D0-VHIS(I)/ICOUNT))
     1      /BOXSIZE / SQRT(DBLE(ICOUNT))
C NORMALIZED VALUE OF THE BAR
           VHIS(I)=VHIS(I)/ICOUNT/BOXSIZE
        ENDDO
        END
       
